plugins {
    // Apply the application plugin to add support for building a Java application
    id 'application'
    id 'java'
}

ext {
    srcLocation = 'src'
    backupLocation = 'backup'
}

// Configure Java toolchain
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17) 
    }
}

application{
    mainClass = 'basic_demo.App'
}

description = "A trivial Gradle build"
version = "0.1.0"

repositories {
    mavenCentral()
}

dependencies {
    // Use Apache Log4J for logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1' // JUnit 5
}

test {
    useJUnitPlatform() // To use JUnit 5
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE // allow duplicates

    manifest {
        attributes "Main-Class": "basic_demo.App"
    }

   from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task backupSources(type: Copy) {
    description = 'Copia os ficheiros da pasta src para uma nova pasta backup'
    group = 'Backup'
    // Diretório de origem (src) e diretório de destino (backup)
    from  project.ext.srcLocation
    into project.ext.backupLocation
    
    // Informa ao Gradle para incluir tudo na pasta src
    include '**/*'
}

task zipClasses(type: Zip, dependsOn: backupSources){
    from(project.ext.backupLocation) {
        include '**/*'
    }
    archiveBaseName = project.name
    archiveExtension = 'zip'
}

task runClient(type:JavaExec, dependsOn: classes){
    group = "DevOps"
    description = "Launches a chat client that connects to a server on localhost:59001 "
  
    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'basic_demo.ChatClientApp'

    args 'localhost', '59001'
}

task runServer(type:JavaExec, dependsOn: classes){
    group = "DevOps"
    description = "Launches a chat server on localhost:59001 "
  
    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'basic_demo.ChatServerApp'

    args '59001'
}